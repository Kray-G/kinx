
_namespace Net {

    Net.TCP = 1;
    Net.UDP = 2;

    enum {
        EAGAIN = 11
    }
    enum {
        SELECT_READ = 0,
        SELECT_WRITE = 1,
        SELECT_READ_WRITE = 2,
    }

    _class AddressPort(con_) {
        public address() {
            return con_.address;
        }
        public port() {
            return con_.port;
        }
        public toString() {
            return con_.address + ":" + con_.port;
        }
    }

    _class TcpSocket(con_) {
        var timeout_;
        private initialize() {
            timeout_ = 100;
        }
        public host() {
            return new AddressPort(con_);
        }
        public setTimeout(msec) {
            timeout_ = msec;
        }
        public isReadable(msec) {
            return con_.select(SELECT_READ, msec ?? timeout_);
        }
        public isWritable(msec) {
            return con_.select(SELECT_WRITE, msec ?? timeout_);
        }
        public send(opts) {
            if (opts.isString) {
                opts = { message: opts };
            }
            var r = con_.send(opts.message);
            if (r < 0) {
                if (isWritable(opts.timeout) > 0) {
                    r = con_.send(opts.message);
                }
                if (r < 0) {
                    throw SocketException("Cannot send message");
                }
            }
        }
        public recv(opts) {
            if (isReadable(opts.timeout)) {
                return con_.recv();
            }
        }
        public close() {
            return con_.close();
        }
    }

    _class UdpSocket(con_) {
        var timeout_;
        var host_;
        private initialize() {
            timeout_ = 100;
        }
        public host() {
            return host_;
        }
        public setTimeout(msec) {
            timeout_ = msec;
        }
        public isReadable(msec) {
            return con_.select(SELECT_READ, msec ?? timeout_);
        }
        public isWritable(msec) {
            return con_.select(SELECT_WRITE, msec ?? timeout_);
        }
        public sendto(opts) {
            if (opts.isString) {
                opts = { message: opts };
            }
            var r = con_.sendto(opts.message);
            if (r < 0) {
                if (isWritable(opts.timeout) > 0) {
                    r = con_.send(opts.message);
                }
                if (r < 0) {
                    throw SocketException("Cannot send message");
                }
            }
        }
        public recvfrom(opts) {
            if (isReadable(opts.timeout)) {
                var r = con_.recvfrom();
                host_ = new AddressPort({ address: r.address, port: r.port });
                return r.message;
            }
        }
        public close() {
            return con_.close();
        }
    }

    _class Socket() {
        var timeout_;
        private initialize() {
            timeout_ = 100;
        }
        public connect(opts) {
            if (opts.udp) {
                var con = Net.createUdpClientSocket(opts.host, "" + opts.port, opts.timeout ?? timeout_);
                return new UdpSocket(con);
            }
            var con = Net.createTcpClientSocket(opts.host, "" + opts.port, opts.timeout ?? timeout_);
            return new TcpSocket(con);
        }
    }

    _class TcpServerSocket(port_) {
        var soc_;
        private initialize() {
            soc_ = Net.createTcpServerSocket("" + port_);
            soc_.Socket = TcpSocket;
            soc_.bind();
            soc_.listen();
        }
        public accept() {
            var con;
            while (true) {
                con = soc_.accept();
                if (con.value >= 0) {
                    break;
                }
                if (con.errno != EAGAIN) {
                    throw SocketException(con.message ?? "Failed to accept");
                }
                System.sleep(100);
            }
            return new soc_.Socket(con);
        }
        public close() {
            return soc_.close();
        }
    }

    _class UdpServerSocket(port_) {
        var con_ = Net.createUdpServerSocket("" + port_);
        con_.bind();
        return new UdpSocket(con_);
    }

} // _namespace Net
