
namespace Net {

    class Http {
        var curl_ = Net.createCurlHandler();
        var dbgCallback_;

        public reset() {
            curl_.resetHandler();
            dbgCallback_ = null;
            curl_.debugDetail = false;
        }

        public sslVerifyPeer(tf) {
            curl_.setOptionInt(Net.CURLOPT_SSL_VERIFYPEER, tf);
        }

        public sslVerifyHost(tf) {
            curl_.setOptionInt(Net.CURLOPT_SSL_VERIFYHOST, tf);
        }

        public setDebugDetail(tf, opts) {
            curl_.debugDetail = tf && ((opts.hex ?? 1) << 1) | tf;
        }

        public setDebug(callback) {
            dbgCallback_ = callback;
            curl_.setOptionInt(Net.CURLOPT_VERBOSE, 1);
        }

        public addHeader(key, value) {
            if (value.isUndefined) {
                curl_.appendSlist("%{key};");
            } else {
                curl_.appendSlist("%{key}: %{value}");
            }
        }

        public removeHeader(key) {
            curl_.appendSlist("%{key}:");
        }

        public setRedirect(tf, opts) {
            curl_.setOptionInt(Net.CURLOPT_FOLLOWLOCATION, tf);
            if (opts.max.isInteger) {
                curl_.setOptionInt(Net.CURLOPT_MAXREDIRS, opts.max);
            }
        }

        private doHttpMethod(callback) {
            var func = callback.isFunction ? callback : System.print;
            do {
                curl_.perform();
                if (curl_.debugInfo.length() > 0) {
                    if (dbgCallback_.isFunction) {
                        dbgCallback_(curl_.debugInfo);
                    }
                    curl_.debugInfo = "";
                }
                if (curl_.received.length() > 0) {
                    func(curl_.received);
                    curl_.received = "";
                }
                curl_.wait(1000);
            } while (curl_.isRunning);
        }

        public get(url, callback) {
            try {
                curl_.setupHandler();
                curl_.setOptionString(Net.CURLOPT_URL, url);
                curl_.setOptionSlist(Net.CURLOPT_HTTPHEADER);
                doHttpMethod(callback);
            } finally {
                curl_.performEnd();
                curl_.setOptionSlist(Net.CURLOPT_HTTPHEADER);
            }
        }
    }

}
