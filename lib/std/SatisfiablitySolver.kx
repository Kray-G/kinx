
_class Satisfiablity(cnf_) {
    public solve() {
        return System.picosatSolve(cnf_);
    }
    public enumerator() {
        return System.picosatGetIter(cnf_);
    }
}

_class SatisfiablityCheckVersion(name_, version_, vs_) {
    public dependsOn(deps) {
        if (!deps.isArray) {
            deps = [deps];
        }
        vs_.setDependency({ name: name_, version: version_ }, deps);
        return this;
    }
}

_class SatisfiablityCheckProduct(name_, vs_) {
    var fixedVersion;
    public name() {
        return name_;
    }
    public version(ver) {
        return new SatisfiablityCheckVersion(name_, ver, vs_);
    }
    public dependsOn(ver, deps) {
        return @version(ver).dependsOn(deps);
    }
    public ()(ver) {
        return { name: name_, version: ver, dependsOn: { => @dependsOn(ver, _1) } };
    }
    public addVersion(version, isFix) {
        if (isFix) {
            if (fixedVersion) {
                // clear the fixed version once.
                vs_.addVersion(name_, fixedVersion, false);
            }
            fixedVersion = version;
        }
        vs_.addVersion(name_, version, isFix);
        return this;
    }
}

_class VersionSatisfiablity() {
    var info_ = [];
    var vers_ = [];
    var nvmp_ = [];
    var deps_ = [];
    public reset() {
        info_ = [];
        vers_ = [];
        nvmp_ = [];
        deps_ = [];
    }
    public addProduct(name) {
        return new SatisfiablityCheckProduct(name, this);
    }
    public addVersion(name, version, isFix) {
        if (nvmp_[name][version].isInteger) {
            var index = nvmp_[name][version] - 1;
            if (info_[index].isDefined && isFix.isDefined) {
                info_[index].isFix = isFix;
            }
            return;
        }
        var index = info_.length() + 1;
        var info = {
            name: name,
            version: version,
            index: index,
            isFix: isFix,
        };
        info_.push(info);
        vers_[name] ??= [];
        vers_[name].push(index);
        nvmp_[name][version] = index;
    }
    public setDependency(base, deps) {
        deps_.push({
            base: base,
            deps: deps,
        });
    }
    private genCnf() {
        var cnf = info_.filter { => _1.isFix }.map { => [_1.index] };
        vers_.keySet().each { &(name)
            var indexes = vers_[name];
            cnf.push(indexes);
            for (var i = 0, l = indexes.length(); i < l; ++i) {
                for (var j = i + 1; j < l; ++j) {
                    cnf.push([-indexes[i], -indexes[j]]);
                }
            }
        };
        deps_.each { &(dep)
            var i1 = nvmp_[dep.base.name][dep.base.version];
            if (!i1) {
                return false;
            }
            var chk = [ -i1 ];
            dep.deps.each { &(d)
                var i2 = nvmp_[d.name][d.version];
                if (i2) {
                    chk.push(i2);
                }
            };
            cnf.push(chk);
        };
        return cnf;
    }
    public solve() {
        var cnf = genCnf();
        var res = System.picosatSolve(cnf);
        return res.filter { => _1 > 0 }.map { => info_[_1 - 1] };
    }
    public enumerator() {
        var cnf = genCnf();
        var iter = System.picosatGetIter(cnf);
        var next = iter.next;
        iter.next = _function() {
            var res = next();
            if (res) {
                return res.filter({ => _1 > 0 }).map { => info_[_1 - 1] };
            }
        };
        return iter;
    }
    public iterator() {
        return this;
    }
}
