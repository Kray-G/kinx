namespace Typesetting {

    const TOO_BIG_SPACE = 6553600.0;
    var hyphenPenalty_ = Hypher.hyphenPenalty_;
    var Linebreak = Typesetting.Linebreak;
    var Word = Typesetting.Word;

    namespace ProcessDoc {

        class Section(info_, context_, util_, paragraph_) {

            public getHeight(level) {
                var lineFeedWidth = info_.base.fontSize[level] * info_.base.lineFeedWidthRatio[level];
                var paragraphSkipWidth = context_.font.fontSize * context_.page.lineFeedWidthRatio;
                var marginSize = info_.base.paragraphSkipWidthRatio[level] * context_.font.fontSize;
                return lineFeedWidth + paragraphSkipWidth + marginSize / 2;
            }

            public exec(level, opts, sectionText) {
                [opts, text] = util_.makeOption(opts, sectionText);

                var writer = info_.writer;
                var sectionNumberText = util_.makeSectionNumber(level).join('.');
                var sectionTextWithNumber = sectionNumberText ? (sectionNumberText + ' ' + text) : text;
                var fontObj = context_.font.fontObj;
                var fontSize = context_.font.fontSize;
                context_.font.fontObj = context_.font.fontObjBase.bold;
                context_.font.fontSize = info_.base.fontSize[level];

                var lineFeedWidthRatio = context_.page.lineFeedWidthRatio;
                context_.page.lineFeedWidthRatio = info_.base.lineFeedWidthRatio[level];
                var paragraphSkipWidth = context_.page.paragraphSkipWidth;
                context_.page.paragraphSkipWidth = context_.font.fontSize * info_.base.paragraphSkipWidthRatio[level];
                if (!writer.isTopOfPage()) {
                    var marginSize = info_.base.paragraphSkipWidthRatio[level] * context_.font.fontSize;
                    writer.setMarginAbove(marginSize / 2);
                }
                var indent = context_.noIndent;
                context_.noIndent = true;
                context_.xrefLabel = sectionText;
                var linetop = writer.getLineTop(context_.font.fontObj, context_.font.fontSize);
                var pagenum = writer.getPageIndex();
                var lines = paragraph_.exec(opts, text, { preText: sectionNumberText || null, preTextSpace: context_.font.fontSize });
                util_.write(lines);
                if (opts.label || context_.xrefLabel) {
                    var xrefInfo = {
                        page:      pagenum,
                        counter:   sectionNumberText,
                        name:      sectionTextWithNumber,
                        text:      text,
                        y:         linetop,
                    };
                    if (opts.label) {
                        context.xref[opts.label] = xrefInfo;
                    }
                    if (context_.xrefLabel) {
                        context_.xref[context_.xrefLabel] = xrefInfo;
                        context_.xrefLabel = null;
                    }
                }
                context_.noIndent = indent;
                context_.page.paragraphSkipWidth = paragraphSkipWidth;
                context_.page.lineFeedWidthRatio = lineFeedWidthRatio;
                context_.font.fontSize = fontSize;
                context_.font.fontObj = fontObj;

                if (info_.base.footnote.resetSectionLevel == level) {
                    context_.counter.footnote = 0;
                }
                var header = (context_.name.section[level] = sectionTextWithNumber);
                if (level <= info_.base.toc.level) {
                    context_.tocinfo.list.push({
                        page: context_.counter.page,
                        level: level,
                        number: sectionNumberText,
                        header: text,
                        y: linetop,
                    });
                }

                var olheader = sectionNumberText + ' ' + sectionText;
                return [linetop, olheader];
            }

        }

    } # namespace ProcessDoc

} # namespace Typesetting
