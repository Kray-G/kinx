namespace Typesetting {

    namespace Component {

        class Image(opts_) : Component.Base(opts_) {
            public setCaption(caption) {
                opts_.caption = caption;
            }
            public write(info, context, core) {
                var lineWidth;
                switch (opts_.box) {
                case "BOX_NORMAL":
                    lineWidth = Typesetting.BOX_NORMAL;
                    break;
                case "BOX_THIN":
                    lineWidth = Typesetting.BOX_THIN;
                    break;
                case "BOX_THICK":
                    lineWidth = Typesetting.BOX_THICK;
                    break;
                }

                if (opts_.float.left || opts_.float.right) {
                    core.flushParagraph();
                    var fontSize = context.font.fontSize;
                    var fontObj = context.font.fontObj;
                    info.writer.loadFloatingImage(
                        { path: opts_.file, scale: opts_.scale, floating: opts_.float, caption: opts_.caption, padding: opts_.padding ?? 0, lineWidth: lineWidth },
                        &(center, y, imageY, height) => @floatingImageCaption(info, core, context, opts_.label, opts_.caption, fontObj, fontSize, center, y, imageY, height));
                    return;
                }

                var left = opts_.align == "left";
                var right = opts_.align == "right";
                var align = {
                    left: left,
                    right: right,
                    center: !left && !right,
                };
                var op = [];
                opts_.keySet().each { &(key):
                    if (opts_[key].isString) {
                        op.push(key + "=" + opts_[key]);
                    }
                };
                return new Typesetting.Component.Paragraph("\\image[%{op.join(',')}]{%{opts_.file}}")
                        .write(info, context, core, align);
            }
        }

    } # namespace Component

} # namespace Typesetting
