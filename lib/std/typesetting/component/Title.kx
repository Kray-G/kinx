namespace Typesetting {

    namespace Component {

        class Title(opts_, title_) : Component.Base(opts_) {
            private initialize() {
                if (title_.isUndefined) {
                    [title_, opts_] = [opts_];
                }
            }
            public setTitle(title, size) {
                title_ = title;
                if (size.isDefined) {
                    opts_.titleSize = size;
                }
            }
            public setTitleSize(size) {
                opts_.titleSize = size;
            }
            public setSubtitle(title, size) {
                opts_.subtitle = title;
                if (size.isDefined) {
                    opts_.subtitleSize = size;
                }
            }
            public setSubtitleSize(size) {
                opts_.subtitleSize = size;
            }
            public setAuthor(author) {
                opts_.author = author;
            }
            public setDate(date) {
                opts_.date = date;
            }
            public write(info, context, core) {
                var writer = info.writer;
                if (info.style.coverPage.isFunction) {
                    info.style.coverPage(info, context, core, title_, opts_);
                    if (info.base.outline) {
                        var ol = writer.createOutline(null, title_, true);
                        writer.setCurrentPage(ol);
                        context.contentsOutline = writer.createOutline(null, info.base.toc.header, true);
                    }
                    return;
                }
                [opts_, title_] = core.util.makeOption(opts_, title_);
                var tbase = info.base.title;
                writer.setColumn(1);
                core.util.writeCenter(opts_, title_, context.font.fontObj, tbase.titleText.fontSize, tbase.titleText.lineFeedWidth);
                if (opts_.author) {
                    core.util.writeCenter(opts_, opts_.author, context.font.fontObj, tbase.author.fontSize, tbase.author.lineFeedWidth);
                }
                if (opts_.date) {
                    core.util.writeCenter(opts_, opts_.date, context.font.fontObj, tbase.date.fontSize, tbase.date.lineFeedWidth);
                }
                writer.addY(tbase.paragraphSkipWidth, true);
                writer.setYHead();
                if (info.base.outline) {
                    var ol = writer.createOutline(null, title_, true);
                    writer.setCurrentPage(ol);
                    context.contentsOutline = writer.createOutline(null, info.base.toc.header, true);
                }
            }
        }

    } # namespace Component

} # namespace Typesetting
