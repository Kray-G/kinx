namespace Typesetting {

    var Linebreak = Typesetting.Linebreak;

    class NullWord(info_) {
        @isNullWord = true;
        var measure_ = info_.measure;
        private initialize() {
        }
        public setWord(fontObj, fontSize, word) {
            return this;
        }
        public fontObj() {
        }
        public fontSize() {
        }
        public getHeight() {
        }
        public textWidth() {
        }
        public spaceWidth() {
        }
        public additionalSpace(width) {
        }
        public toString() {
            return "{NullWord}";
        }
        public display(indent) {
        }
    }

    class Vspace(info_) : NullWord {
        @isVspace = true;
        private initialize() {
            @word = "";
        }
        public setSpace(spaceText) {
            @spaceText = spaceText;
            @space = Typesetting.Util.getPt(info_.context, spaceText);
            return this;
        }
        public toString() {
            return "{Vspace:%{[@spaceText,@space]}}";
        }
        public display(indent) {
        }
    }

    class InlineImage(info_) {
        @isInlineImage = true;
        var measure_ = info_.measure;
        var writer_ = info_.writer;
        public setImage(imgfile, opts) {
            var scale = Double.parseDouble(opts.scale);
            @word = "<InlineImage>";
            fontObj ??= info_.context.font.fontObj;
            fontSize ??= info_.context.font.fontSize;
            @image = imgfile.isString ? writer_.loadImage(imgfile) : imgfile;
            @width = @image.getWidth();
            @height = @image.getHeight();
            var lineFeedWidth = info_.context.page.lineFeedWidthRatio * fontSize;
            var lineHeight = lineFeedWidth;
            if (!scale) {
                scale = lineHeight / @height;
            }
            @width = @width * scale;
            @height = @height * scale;
            @offsetY = opts.offsetY ? Double.parseDouble(opts.offsetY) : 0;
            @bottom = @offsetY;
            @top = @height + @bottom;
            return this;
        }
        public fontObj() {
        }
        public fontSize() {
        }
        public getHeight() {
            return @height;
        }
        public textWidth() {
            return @width;
        }
        public spaceWidth() {
            return @addSpace;
        }
        public additionalSpace(width) {
            @addSpace = width;
        }
        public toString() {
            return "{Image}";
        }
        public display(indent) {
        }
        public flatten() {
            return this;
        }
    }

    class Image(info_) {
        @isImage = true;
        var context_ = info_.context;
        var measure_ = info_.measure;
        var writer_ = info_.writer;
        public setImage(imgfile, scale, caption) {
            @word = "<FullWidthImage>";
            @lineBreak = true;
            @image = writer_.loadImage(imgfile);
            var margin = writer_.makeMarginSize();
            var width = @image.getWidth();
            var height = @image.getHeight();
            scale ??= 1.0;
            scale *= (margin.width / width);
            if (caption) {
                var level = context_.counter.image[0];
                var num = ++context_.counter.image[1];
                var lt = [];
                for (var i = 1; i <= level; ++i) {
                    lt.push(context_.counter.section[i]);
                }
                lt.push("" + num);
                @captionNumber = lt.join('.');
                @captionText = caption;
                @captionHeader = info_.base.image.label + ' ' + @captionNumber;
                @fullHeader = info_.base.image.fulllabel + ' ' + @captionNumber;
                @caption =  + ' ' + caption;
            }
            @fontObj = context_.font.fontObj;
            @fontSize = context_.font.fontSize;
            @width = width * scale;
            @height = height * scale;
            @lineFeedWidth = @fontSize * context_.page.lineFeedWidthRatio;
            @scale = scale;
            @bottom = 0;
            @top = @height;
            return this;
        }
        public setBox(lineWidth, padding) {
            @lineWidth = lineWidth;
            padding = padding.isUndefined ? 0.0 : Double.parseDouble(padding);
            @padding = padding;
        }
        public fontObj() {
        }
        public fontSize() {
        }
        public getHeight() {
            return @height;
        }
        public textWidth() {
            return @width;
        }
        public spaceWidth() {
            return @addSpace;
        }
        public additionalSpace(width) {
            @addSpace = width;
        }
        public toString() {
            return "{Image}";
        }
        public display(indent) {
        }
        public flatten() {
            return this;
        }
        private appendListOf(target, level, number, header, linetop) {
            context_.tocinfo[target] ??= {};
            context_.tocinfo[target].push({
                page: context_.counter.page,
                level: level,
                number: number,
                header: header,
                y: linetop,
            });
        }
        public write(writer, x, y, isFloatingLine, align) {
            var linetop = writer.getLineTop(@fontObj, @fontSize);
            writer_.imageTo(@image, @scale, align, { lineWidth: @lineWidth, padding: @padding });
            if (@caption) {
                writer_.lineBreak(@lineFeedWidth);
                writer_.writeCenter(@caption, @fontObj, @fontSize);
                writer_.lineBreak(@lineFeedWidth);
                appendListOf("lof", info_.base.image.level + 1, @fullHeader, @captionText, linetop);
            }
        }
    }

    class Word(info_) {
        @isWord = true;
        var context_ = info_.context;
        var measure_ = info_.measure;
        private initialize() {
            @word = '';
            @offset = 0.0;
            @space = 0.0;
            @addSpace = 0.0;
        }
        public setWord(fontObj, fontSize, word, width) {
            @word = word;
            @isUtf8 = System.isUtf8Bytes(word[0]) > 1;
            @font = fontObj;
            @size = fontSize;
            @width = width ?? measure_.textWidth(@font, fontSize, word);
            @space = measure_.spaceWidth(@font, fontSize);
            @offsetX = context_.offsetX;
            @offsetY = context_.offsetY;
            var descent = fontObj.getDescent();
            @bottom = descent * fontSize / 1000;
            var ascent = fontObj.getAscent();
            @top = ascent * fontSize / 1000;
            @color = info_.context.font.color;
            @underline = info_.context.font.underline;
            return this;
        }
        public fontObj() {
            return @font;
        }
        public fontSize() {
            return @size ?? 0.0;
        }
        public getHeight() {
            return info_.measure.getAscent(@font, @size);
        }
        public textWidth() {
            return @width ?? 0.0;
        }
        public spaceWidth() {
            return @space + @addSpace;
        }
        public additionalSpace(width) {
            @addSpace = width;
        }
        public toString() {
            return "{'%{@word}',%{@font.getFontName()},%{[@size,@width]}, offset:[%{@offsetX},%{@offsetY}]}";
        }
        public display(indent) {
            indent ??= 0;
            if (indent) {
                System.print("    " * indent);
            }
            System.println(@toString());
        }
        public flatten() {
            return this;
        }
    }

    # WordSet is a set of Word with space, or WordSet itself recursively.
    class WordSet(info_) {
        @isWordSet = true;
        private initialize() {
            @words = [];
        }
        public addWord(wordObj) {
            if (wordObj.isArray) {
                @words += wordObj;
            } else {
                @words.push(wordObj);
            }
            return this;
        }
        public fontObj() {
            return @words.length() > 0 ? @words[-1].fontObj() : null;
        }
        public fontSize() {
            return @words.length() > 0 ? @words[-1].fontSize() : 0.0;
        }
        public getHeight() {
            return 0;
        }
        public textWidth() {
            var sum = 0.0;
            var words = @words;
            var len = words.length();
            var last = len - 1;
            for (var i = 0; i < len; ++i) {
                sum += words[i].textWidth();
                if (i != last) {
                    sum += words[i].spaceWidth();
                }
            }
            return sum;
        }
        public additionalSpace(width) {
            if (@words.length() > 0) {
                @words[-1].additionalSpace(width);
            }
        }
        public spaceWidth() {
            return @words.length() > 0 ? @words[-1].spaceWidth() : 0.0;
        }
        public toString() {
            return @words.map { => _1.toString() }.toString();
        }
        public display(indent) {
            indent ??= 0;
            if (indent) {
                System.print("    " * indent);
            }
            System.println("WordSet");
            @words.each { &(word):
                word.display(indent + 1);
            };
        }
        public flatten() {
            var bottom, top;
            var result = [];
            @words.each { &(word, i):
                var wd = word.isBox ? word.value : word;
                if (wd.isWordSet) {
                    result += wd.flatten();
                    if (wd.bottom < bottom) {
                        bottom = wd.bottom;
                    }
                    if (top < wd.top) {
                        top = wd.top;
                    }
                } else if (word.isWord) {
                    result.push(Linebreak.Box(word.width, word));
                    if (word.bottom < bottom) {
                        bottom = word.bottom;
                    }
                    if (top < word.top) {
                        top = word.top;
                    }
                } else if (word.isDefined && !word.isNullWord) {
                    result.push(word);
                    if (word.bottom < bottom) {
                        bottom = word.bottom;
                    }
                    if (top < word.top) {
                        top = word.top;
                    }
                }
            };
            if ((@isUrl || @isAnnot) && result.length() > 0) {
                var last;
                var rl = result.length();
                for (var i = 0; i < rl; ++i) {
                    if (result[i].isBox) {
                        if (@isUrl) {
                            if (last.isUndefined) {
                                result[i].value.startUrl = true;
                            }
                        } else {
                            result[i].value.inAnnot = true;
                            result[i].value.annotPage = @annotPage;
                            result[i].value.annotPos = @annotPos;
                            if (last.isUndefined) {
                                result[i].value.startAnnot = true;
                            }
                        }
                        last = i;
                    }
                }
                if (last >= 0) {
                    if (@isUrl) {
                        result[last].value.endUrl = true;
                        result[last].value.link = @link;
                    } else {
                        result[last].value.endAnnot = true;
                    }
                    result[last].value.bottom = bottom;
                    result[last].value.top = top;
                }
            }
            @bottom = bottom;
            @top = top;
            return result;
        }
    }

} # namespace Typesetting
