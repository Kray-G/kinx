using PdfCore;
using typesetting.LineBreak;
using typesetting.Measurement;
using typesetting.PdfWriter;
using typesetting.ParagraphParser;
using typesetting.lang;

namespace Typesetting {

    var Linebreak = Typesetting.Linebreak;
    var hyphenPenalty_ = Hypher.hyphenPenalty_;
    var Word = Typesetting.Word;

    class DocumentUtil(info_, context_, doc_) {

        var defs_ = [];
        var opts_;

        public add(name, value) {
            defs_.unshift({
                regex: new Regex("\\\\" + name + "\\b"),
                value: value,
            });
        }

        public setOpts(opts) {
            opts_ = opts;
        }

        public makeOption(opts, text, replaceText) {
            if (text.isUndefined && opts.isString) {
                text = opts;
                opts = null;
            }
            if (opts_ && !opts.isObject) {
                opts = opts_;
            }
            if (text && (replaceText.isUndefined || replaceText)) {
                defs_.each {
                    text = text.replace(_1.regex, _1.value);
                };
            }
            return [opts, text];
        }

        public makeMargin(opts) {
            var margin = info_.base.margin.clone();
            if (opts.margin.left) {
                margin.left = opts.margin.left;
            }
            if (opts.margin.right) {
                margin.right = opts.margin.right;
            }
            return margin;
        }

        public write(lines) {
            if (lines.length() == 0) {
                return;
            }
            var writer = info_.writer;
            var pos = writer.getPositionBase();
            var y = pos.y;
            var y1 = pos.y;
            var bottom = pos.bottom;
            var ll = lines.length();
            var widowCheck = ll - info_.base.widowPenalty;
            var doColumnBreak;
            if (y > 0) {
                for (var i = 0; i < widowCheck; ++i) {
                    y -= lines[i].lineFeedWidth;
                }
                for (var i = widowCheck; i < ll; ++i) {
                    y -= lines[i].lineFeedWidth;
                    if (y < bottom) {
                        doColumnBreak = true;
                        break;
                    }
                }
            }
            --widowCheck;
            var lastLineFeedWidth = writer.getLastLineFeedWidth();
            if (lastLineFeedWidth < lines[0].height) {
                writer.addY(lines[0].height - lastLineFeedWidth);
            }
            for (var i = 0, ll = lines.length(); i < ll; ++i) {
                var line = lines[i];
                if (doColumnBreak && widowCheck == i) {
                    writer.columnBreak();
                }
                line.nodes.each { => writer.write(_1) };
                var colBreaked = writer.lineBreak(line.lineFeedWidth);
                if (colBreaked) {
                    doColumnBreak = false;
                }
            }
        }

        public writeCenter(opts, text, fontObj, fontSize, lineFeedWidth) {
            var textWidth = info_.measure.textWidth(fontObj, fontSize, text);
            var writer = info_.writer;
            var margin = writer.makeMarginSize();
            var x = (margin.width - textWidth) / 2 + margin.left;
            var n = Linebreak.Box(textWidth, new Word(info_).setWord(fontObj, fontSize, text));
            writer.write(n, x);
            writer.lineBreak(lineFeedWidth);
            writer.setYHead();
        }

        public writeLine(opts, text, fontObj, fontSize, lineFeedWidth) {
            var writer = info_.writer;
            var textWidth = info_.measure.textWidth(fontObj, fontSize, text);
            var margin = writer.makeMarginSize();
            var x = opts.margin.left ?? margin.left;
            var n = Linebreak.Box(textWidth, new Word(info_).setWord(fontObj, fontSize, text));
            writer.write(n, x);
            writer.lineBreak(lineFeedWidth);
        }

    }

    class Paragraph(info_, context_, doc_, util_) {

        private setLineInfo(lines) {
            var lineFeedWidthRatio = context_.page.lineFeedWidthRatio;
            var llast = lines.length() - 1;
            if (llast >= 0) {
                lines[-1].lineFeedWidth = context_.page.paragraphSkipWidth; 
            }
            lines.each { &(line, li):
                var lineFeedWidth = 0;
                var maxHeight = 0;
                line.nodes.each { &(n, i):
                    var value = n.value;
                    var word = value ? value.word : '';
                    if (n.isBox && word != '') {
                        var lfs = lineFeedWidthRatio * value.size;
                        if (lineFeedWidth < lfs) {
                            lineFeedWidth = lfs;
                        }
                    }
                    var value = n.value;
                    if (value.isWord) {
                        var fontSize = value.fontSize();
                        var fontObj = value.fontObj();
                        var height = info_.measure.getAscent(fontObj, fontSize);
                        if (maxHeight < height) {
                            maxHeight = height;
                        }
                    }
                };
                if (li > 0) {
                    lines[li-1].lineFeedWidth = lineFeedWidth;
                }
                line.height = maxHeight;
            };
        }

        private adjustLines(lines, doJustification) {
            doJustification ??= true;
            var llast = lines.length() - 1;
            lines.each { &(line, li):
                var x1 = 0.0;
                var spaces = 0;
                var expected = line.width;
                var alen = line.nodes.length();
                var alast = alen - 1;
                line.nodes.each { &(n, i):
                    var value = n.value;
                    var word = value ? value.word : '';
                    if (n.isBox && word != '') {
                        x1 += n.width;
                    } else if (n.isBox && word == '') {
                        # indent.
                        x1 += n.width;
                    } else if (n.isGlue && i != alast) {
                        x1 += n.width;
                        if (n.stretch > 0 || n.shrink > 0) {
                            ++spaces;
                        }
                    } else if (n.isGlue) {
                        # end of line.
                        ;
                    } else if (n.isPenalty && n.penalty == hyphenPenalty_ && i == alast) {
                        x1 += n.width;
                    }
                };
                var prev;
                var diff = doJustification && spaces > 0 ? ((x1 - expected) / spaces) : 0;
                x1 = 0.0;
                line.nodes.each(function(n, i) {
                    var d = li == llast ? 0 : diff;
                    var value = n.value;
                    var word = value ? value.word : '';
                    if (n.isBox && word != '') {
                        value.x = x1;
                        x1 += n.width;
                    } else if (n.isBox && word == '') {
                        x1 += n.width;
                    } else if (n.isGlue && i != alast) {
                        if (n.stretch > 0 || n.shrink > 0) {
                            n.width -= d;
                        }
                        x1 += n.width;
                    } else if (n.isGlue) {
                        n.width = 0.0;
                    } else if (n.isPenalty && n.penalty == hyphenPenalty_ && i == alast) {
                        value.x = x1;
                        n.value.word = (prev.isDefined && prev.isBox && prev.value.word[-1] != '-'[0]) ? "-" : "";
                        x1 += n.width;
                    } else {
                        n.width = 0.0;
                    }
                    prev = n;
                });
            };
        }

        private processWordSet(opts, wordSet, fontObj, fontSize, areaWidth, settings) {
            if (!areaWidth.isArray) {
                areaWidth = [areaWidth];
            }
            var words = wordSet.flatten();
            var nodes = [];
            if (!context_.noIndent) {
                nodes.push(Linebreak.Box(info_.base.indentSize, new Word(info_).setWord(fontObj, fontSize, '')));
            }
            if (settings.preText) {
                var textWidth = info_.measure.textWidth(fontObj, fontSize, settings.preText);
                nodes.push(Linebreak.Box(textWidth, new Word(info_).setWord(fontObj, fontSize, settings.preText)));
                if (settings.preTextSpace) {
                    var width = settings.preTextSpace;
                    var stretch = (fontSize * 3) / 6;
                    var shrink = (fontSize * 3) / 9;
                    nodes.push(Linebreak.Glue(width, stretch, shrink));
                }
            }
            Typesetting.insertGlue.ja(info_, Linebreak, words, nodes);
            if (nodes[-1].isGlue) {
                nodes.pop();
            }
            nodes.push(Linebreak.Glue(0, Linebreak.Infinity, 0));
            nodes.push(Linebreak.Penalty(0, -Linebreak.Infinity, 1));
# nodes.each { => _1.display() };
            var breaks;
            for (var i = 1; i < 10; ++i) {
                breaks = new Linebreak(nodes, areaWidth, { tolerance: i }).parse();
                if (breaks.length() > 0) {
                    break;
                }
            }

            var areaWidthLen = areaWidth.length();
            var lines = [];
            if (breaks.length() == 0) {
                var currentWidth, ci = 0;
                var spaceWidth = info_.measure.spaceWidth(fontObj, fontSize);
                var nl = nodes.length();
                var ns = [];
                var x = 0.0, i;
                for (i = 0; i < nl; ++i) {
                    currentWidth = ci < areaWidthLen ? areaWidth[ci] : areaWidth[-1];
                    var n = nodes[i];
                    x += n.width;
                    if (x < currentWidth) {
                        ns.push(n);
                    } else {
                        lines.push({
                            ratio: 0,
                            nodes: ns,
                            position: 0,
                            width: currentWidth,
                            lineFeedWidth: 0,
                        });
                        ns = [];
                        ns.push(n);
                        x = n.width;
                        ++ci;
                    }
                }
                if (ns.length() > 0) {
                    currentWidth = ci < areaWidthLen ? areaWidth[ci] : areaWidth[-1];
                    lines.push({
                        ratio: 0,
                        nodes: ns,
                        position: 0,
                        width: currentWidth,
                        lineFeedWidth: 0,
                    });
                }
                # # TODO: warning.
                System.println("Warning: no lines...; ", lines.length());
                # System.println("----");
                # System.println(text);
                # System.println("----");
            } else {
                var lineStart = 0, ls = 0;
                for (var i = 1, blen = breaks.length(); i < blen; ++i) {
                    var point = breaks[i].position;
                    var r = breaks[i].ratio;
                    for (var j = lineStart, nlen = nodes.length(); j < nlen; ++j) {
                        # After a line break, we skip any nodes unless they are boxes or forced breaks.
                        if (nodes[j].isBox || (nodes[j].isPenalty && nodes[j].penalty == -Linebreak.Infinity)) {
                            lineStart = j;
                            break;
                        }
                    }
                    lines.push({
                        ratio: r,
                        nodes: nodes.subArray(lineStart, point + 1 - lineStart),
                        position: point,
                        width: ls < areaWidthLen ? areaWidth[ls] : areaWidth[-1],
                        lineFeedWidth: 0,
                    });
                    lineStart = point;
                    ++ls;
                }
            }
            if (opts.label.isBox) {
                lines[0].nodes.unshift(opts.label);
            }
            setLineInfo(lines);
            adjustLines(lines);
            return lines;
        }

        public exec(opts, text, settings) {
            [opts, text] = util_.makeOption(opts, text, false);

            var wordSet = info_.paragraphParser.parse(text);
            if (context_.footnotes.length() > 0) {
                var margin = util_.makeMargin({ margin: { left: margin.left + info_.base.footnote.left } });
                info_.writer.setMargin(margin);
                var marginSize = info_.writer.makeMarginSize();
                var fontSize = info_.base.footnote.fontSize;
                var lineFeedWidthRatio = context_.page.lineFeedWidthRatio;
                context_.page.lineFeedWidthRatio = info_.base.footnote.lineFeedWidthRatio;
                var paragraphSkipWidth = context_.page.paragraphSkipWidth;
                context_.page.paragraphSkipWidth = info_.base.footnote.lineFeedWidthRatio * fontSize;
                var indent = context_.noIndent;
                context_.noIndent = true;
                context_.footnotes.each { &(wordSet):
                    var number = wordSet.footnoteNumber;
                    var lines = processWordSet({}, wordSet, context_.font.fontObj, fontSize, [marginSize.width], settings);
                    var lineFeedWidth = lines.reduce { &(r, e) => r + e.lineFeedWidth };
                    info_.writer.addFootnote({
                        number: number,
                        lines: lines,
                        lineFeedWidth: lineFeedWidth,
                    });
                };
                context_.noIndent = indent;
                context_.page.paragraphSkipWidth = paragraphSkipWidth;
                context_.page.lineFeedWidthRatio = lineFeedWidthRatio;
                context_.footnotes = [];
            }

            var margin = util_.makeMargin(opts);
            info_.writer.setMargin(margin);
            var areaWidth = info_.writer.getAreaWidth();
            return processWordSet(opts, wordSet, context_.font.fontObj, context_.font.fontSize, areaWidth, settings);
        }

    }

    class Section(info_, context_, doc_, util_, paragraph_) {

        public getHeight(level) {
            var lineFeedWidth = info_.base.fontSize[level] * info_.base.lineFeedWidthRatio[level];
            var paragraphSkipWidth = context_.font.fontSize * context_.page.lineFeedWidthRatio;
            var marginSize = info_.base.paragraphSkipWidthRatio[level] * context_.font.fontSize;
            return lineFeedWidth + paragraphSkipWidth + marginSize / 2;
        }

        private makeSectionNumber(level) {
            var sectionNumber = context_.sectionNumber[level] ?? 0;
            var numText = [];
            if (context_.check) {
                for (var i = 1, l = context_.sectionNumber.length(); i < l; ++i) {
                    if (i <= level) {
                        numText.push(context_.sectionNumber[i]);
                    }
                }
            } else {
                context_.sectionNumber[level] = ++sectionNumber;
                var newNumbers = [];
                for (var i = 1, l = context_.sectionNumber.length(); i < l; ++i) {
                    if (i <= level) {
                        newNumbers[i] = context_.sectionNumber[i];
                        numText.push(newNumbers[i]);
                    } else {
                        newNumbers[i] = 0;
                    }
                }
                context_.sectionNumber = newNumbers;
            }
            
            return numText;
        }

        public exec(level, opts, text) {
            [opts, text] = util_.makeOption(opts, text);

            var numText = makeSectionNumber(level);
            var sectionNumberText = numText.join('.');
            var font = context_.font;
            context_.font = {
                fontObj: context_.font.fontObjBase.bold,
                fontSize: info_.base.fontSize[level],
            };

            var lineFeedWidthRatio = context_.page.lineFeedWidthRatio;
            context_.page.lineFeedWidthRatio = info_.base.lineFeedWidthRatio[level];
            var paragraphSkipWidth = context_.page.paragraphSkipWidth;
            context_.page.paragraphSkipWidth = context_.font.fontSize * info_.base.paragraphSkipWidthRatio[level];
            if (!info_.writer.isTopOfPage()) {
                var marginSize = info_.base.paragraphSkipWidthRatio[level] * context_.font.fontSize;
                info_.writer.setMarginAbove(marginSize / 2);
            }
            var indent = context_.noIndent;
            context_.noIndent = true;
            var lines = paragraph_.exec(opts, text, { preText: sectionNumberText, preTextSpace: context_.font.fontSize });
            util_.write(lines);
            context_.noIndent = indent;
            context_.page.paragraphSkipWidth = paragraphSkipWidth;
            context_.page.lineFeedWidthRatio = lineFeedWidthRatio;
            context_.font = font;
            return sectionNumberText + ' ' + text;
        }

    }

    class Itemize(info_, context_, doc_, util_, paragraph_) {

        var itemizeImpl;

        private itemizeItem(opts, items, level, itemType, itemMarginBase, itemMargin, writer, margin) {
            var bullet = items.label ?? (info_.base.itemize.itemLabels[itemType][level] || '-');
            var textWidth = info_.measure.textWidth(context_.font.fontObj, context_.font.fontSize, bullet);
            var label = new Word(info_).setWord(context_.font.fontObj, context_.font.fontSize, bullet);
            label.offsetX = -info_.base.itemize.labelsep[level] - textWidth;
            var n = Linebreak.Box(0, label);
            var itemx = itemMarginBase + itemMargin;
            var itemlist = items.items;
            var ll = itemlist.length();
            for (var i = 0; i < ll; ++i) {
                var item = itemlist[i];
                if (item.items.isArray) {
                    item.level = level + 1;
                    itemizeImpl({ margin: margin, itemMarginBase: itemx }, item);
                } else if (item.isString) {
                    [opts, item] = util_.makeOption(opts, item);
                    var lines = paragraph_.exec({ label: n, margin: { left: margin.left + itemx } }, item || "((none))");
                    util_.write(lines);
                }
            }
        }

        private itemizeEnum(opts, items, level, itemType, counterInit, itemMarginBase, itemMargin, writer, margin) {
            var numberFormat = items.label ?? (info_.base.itemize.itemLabels[itemType][level] || '1. ');
            var format, counter, getCounter;
            if (numberFormat.find('1') >= 0) {
                counter = 1;
                format = '1';
                getCounter = function() { return counter + 1; };
            } else if (numberFormat.find('a') >= 0) {
                counter = 'a';
                format = 'a';
                getCounter = function() { return counter.next(); };
            } else if (numberFormat.find('i') >= 0) {
                var counterBase = 1;
                counter = Typesetting.Util.romanize(1);
                format = 'i';
                getCounter = function() { return Typesetting.Util.romanize(++counterBase); };
            } else if (numberFormat.find('I') >= 0) {
                var counterBase = 1;
                counter = Typesetting.Util.romanizeCap(1);
                format = 'i';
                getCounter = function() { return Typesetting.Util.romanizeCap(++counterBase); };
            } else if (numberFormat.find('A') >= 0) {
                counter = 'A';
                format = 'A';
                getCounter = function() { return counter.next(); };
            }
            while (--counterInit) {
                counter = getCounter();
            }
            var itemx = itemMarginBase + itemMargin;
            var itemlist = items.items;
            var ll = itemlist.length();
            for (var i = 0; i < ll; ++i) {
                var item = itemlist[i];
                if (item.items.isArray) {
                    item.level = level + 1;
                    itemizeImpl({ margin: margin, itemMarginBase: itemx }, item);
                } else if (item.isString) {
                    var number = numberFormat.replace(format, ""+counter);
                    var textWidth = info_.measure.textWidth(context_.font.fontObj, context_.font.fontSize, number);
                    var label = new Word(info_).setWord(context_.font.fontObj, context_.font.fontSize, number);
                    label.offsetX = -info_.base.itemize.labelsep[level] - textWidth;
                    var n = Linebreak.Box(0, label);
                    [opts, item] = util_.makeOption(opts, item);
                    var lines = paragraph_.exec({ label: n, margin: { left: margin.left + itemx } }, item || "((none))");
                    util_.write(lines);
                    counter = getCounter();
                }
            }
        }

        private itemizeImpl(opts, items) {
            if (items.isUndefined) {
                items = opts;
                opts = null;
            }
            var level = items.level ?? 0;
            var itemType = items.labelType ?? 'item';
            var fontSize = context_.font.fontSize;
            context_.font.fontSize = info_.base.itemize.fontSize[level];
            var indent = context_.noIndent;
            context_.noIndent = true;
            var paragraphSkipWidth = context_.page.paragraphSkipWidth;
            context_.page.paragraphSkipWidth = info_.base.itemize.paragraphSkipWidth[level];
            var lineFeedWidthRatio = context_.page.lineFeedWidthRatio;
            context_.page.lineFeedWidthRatio = info_.base.itemize.lineFeedWidthRatio[level];
            var itemMargin = info_.base.itemize.margin[level];
            var itemMarginBase = opts.itemMarginBase ?? 0;
            var margin = util_.makeMargin(opts);
            if (itemType == "item") {
                itemizeItem(opts, items, level, itemType,
                    itemMarginBase,
                    itemMargin,
                    info_.writer,
                    margin);
            } else if (itemType == "enum") {
                itemizeEnum(opts, items, level, itemType, items.counter || 1,
                    itemMarginBase,
                    itemMargin,
                    info_.writer,
                    margin);
            }

            context_.page.lineFeedWidthRatio = lineFeedWidthRatio;
            context_.page.paragraphSkipWidth = paragraphSkipWidth;
            context_.font.fontSize = fontSize;
            context_.noIndent = indent;
        }

        public exec(opts, items) {
            itemizeImpl(opts, items);
            var lineFeedWidthRatio = info_.base.itemize.lineFeedWidthRatio[0];
            var fontSize = info_.base.itemize.fontSize[0];
            info_.writer.addY(lineFeedWidthRatio * fontSize / 2, false);
        }

    }

    class Document(options_) {
        var info_, context_, outline_;
        var fontObjSerifBase_, fontObjSansBase_;
        var fontObjSerif_, fontObjSans_;
        var fontObjMonotype_, fontObjMonotypeBase_;
        var pendings_ = [];
        var core_ = {};

        private initialize() {
            if (options_.create.isFunction) {
                options_ = new options_().style;
            }
            info_.base = options_.clone();
            outline_ = info_.base.outline;
            var pagesize = options_.pagesize ?? PDF_PAGE_SIZE_A4;
            var direction = options_.direction ?? PDF_PAGE_PORTRAIT;
            var fontSize = options_.fontSize[0] ?? 10.0;
            var writer = new Typesetting.PdfWriter(pagesize, direction, info_);
            [fontObjSerif_, fontObjSans_, fontObjMonotype_, fontObjSerifBase_, fontObjSansBase_, fontObjMonotypeBase_] = writer.getFontObjBase();
            var measure = new Typesetting.Measurement(writer.getPdf(), writer.getPage());
            info_.measure = measure;
            info_.writer = writer;
            writer.setPageMargin(info_.base.margin);
            var pageSize = measure.getPageSize();
            context_ = {
                noIndent: false,
                commands: [],
                outline: [],
                sectionNumber: [],
                imageCounter: [info_.base.sectionLevel, 0],
                footnotes: [],
                footnoteCounter: 0,
                page: {
                    width: pageSize.width,
                    height: pageSize.height,
                    paragraphSkipWidth: info_.base.paragraphSkipWidthRatio[0] * fontSize,
                    lineFeedWidthRatio: info_.base.lineFeedWidthRatio[0],
                },
                font: {
                    fontObj: fontObjSerif_,
                    fontObjBase: fontObjSerifBase_,
                    fontSize: fontSize,
                    footnoteSize: info_.base.footnoteSize,
                },
                fontSet: {
                    serif: {
                        fontObj: fontObjSerif_,
                        fontObjBase: fontObjSerifBase_,
                    },
                    sans: {
                        fontObj: fontObjSans_,
                        fontObjBase: fontObjSansBase_,
                    },
                    monotype: {
                        fontObj: fontObjMonotype_,
                        fontObjBase: fontObjMonotypeBase_,
                    },
                },
            };
            info_.paragraphParser = new Typesetting.ParagraphParser(context_, info_);
            info_.context = context_;

            core_.util = new DocumentUtil(info_, context_, this);
            core_.paragraph = new Paragraph(info_, context_, this, core_.util);
            core_.section = new Section(info_, context_, this, core_.util, core_.paragraph);
            core_.itemize = new Itemize(info_, context_, this, core_.util, core_.paragraph);

            @define("TeX", %!T\hspace[width=-0.152em]\lower[height=0.171em]{E}\hspace[width=-0.084em]X!);
            @define("LaTeX", %!L\hspace[width=-0.3em]\raise[height=0.3ex]{\font[size=0.8em]{A}}\hspace[width=-0.132em]\TeX!);
            @define("KaTeX", %!K\hspace[width=-0.2em]\raise[height=0.3ex]{\font[size=0.8em]{A}}\hspace[width=-0.132em]\TeX!);
        }

        public define(name, value) {
            core_.util.add(name, value);
        }

        private flushParagraph() {
            var writer = info_.writer;
            var totalLineFeeds = pendings_.sum({ => _1.lineFeedWidth ?? 0 });
            writer.checkColumnBreak(totalLineFeeds);
            pendings_.each {
                if (!_1.isParagraph) {
                    writer.clearFloatingImage(_1.lineFeedWidth);
                }
                _1.func();
            };
            pendings_ = [];
        }

        public setColumn(column) {
            flushParagraph();
            info_.writer.setColumn(column);
        }

        public columnBreak() {
            flushParagraph();
            info_.writer.columnBreak();
        }

        public pageBreak() {
            flushParagraph();
            info_.writer.pageBreak();
        }

        public title(opts, text) {
            [opts, text] = core_.util.makeOption(opts, text);
            info_.writer.setColumn(1);
            core_.util.writeCenter(opts, text, context_.font.fontObj, info_.base.title.fontSize, info_.base.title.lineFeedWidth);
            if (outline_) {
                var ol = info_.writer.createOutline(null, text, true);
                info_.writer.setCurrentPage(ol);
            }
        }

        public abstract(opts, text) {
            [opts, text] = core_.util.makeOption(opts, text);
            var fontObj = context_.font.fontObj;
            context_.font.fontObj = context_.font.fontObjBase.bold;
            var titleText = info_.base.abstract.title;
            core_.util.writeCenter(opts, titleText, context_.font.fontObj, info_.base.abstract.fontSize, info_.base.abstract.lineFeedWidth);
            context_.font.fontObj = fontObj;
            var writer = info_.writer;
            var pageWidth = writer.getPage().getWidth();
            var margin = core_.util.makeMargin(opts);
            var width = (pageWidth - margin.left - margin.right) * info_.base.abstract.widthRatio;
            var marginWidth = (pageWidth - width) / 2;
            @set({ margin: { left: marginWidth, right: marginWidth } }) {
                var lines = core_.paragraph.exec(opts, text);
                core_.util.write(lines);
            };
            writer.addY(info_.base.abstract.paragraphSkipWidth, true);
            if (outline_) {
                var ol = info_.writer.createOutline(null, titleText, true);
                info_.writer.setCurrentPage(ol);
            }
        }

        public chapter(opts, text) {
            if (info_.base.sectionLevel <= 1) {
                throw RuntimeException("This style do not support a chapter");
            }
            info_.writer.setColumn(1);
            pendings_.push({
                lineFeedWidth: core_.section.getHeight(1),
                func: &() => {
                    var header = core_.section.exec(1, opts, text);
                    if (outline_) {
                        var ol = info_.writer.createOutline(null, header, true);
                        info_.writer.setCurrentPage(ol);
                        outline[0] = ol;
                    }
                },
            });
        }

        public section(opts, text) {
            info_.writer.setColumn(info_.base.columns);
            pendings_.push({
                isSection: true,
                lineFeedWidth: core_.section.getHeight(1),
                func: &() => {
                    var level = info_.base.sectionLevel;
                    var header = core_.section.exec(level, opts, text);
                    if (outline_) {
                        var ol = info_.writer.createOutline(context_.outline[level - 1], header, true);
                        info_.writer.setCurrentPage(ol);
                        context_.outline[level] = ol;
                    }
                },
            });
        }

        public subSection(opts, text) {
            pendings_.push({
                isSection: true,
                lineFeedWidth: core_.section.getHeight(2),
                func: &() => {
                    var level = info_.base.sectionLevel + 1;
                    var header = core_.section.exec(level, opts, text);
                    if (outline_) {
                        var ol = info_.writer.createOutline(context_.outline[level - 1], header, true);
                        info_.writer.setCurrentPage(ol);
                        context_.outline[level] = ol;
                    }
                },
            });
        }

        public subSubSection(opts, text) {
            pendings_.push({
                isSection: true,
                lineFeedWidth: core_.section.getHeight(3),
                func: &() => {
                    var level = info_.base.sectionLevel + 2;
                    var header = core_.section.exec(level, opts, text);
                    if (outline_) {
                        var ol = info_.writer.createOutline(context_.outline[level - 1], header, true);
                        info_.writer.setCurrentPage(ol);
                        context_.outline[level] = ol;
                    }
                },
            });
        }

        public paragraph(opts, text) {
            [opts, text] = core_.util.makeOption(opts, text);
            var lines = core_.paragraph.exec(opts, text);
            var lineFeedWidth = 0;
            var ll = lines.length();
            var op = info_.base.orphanPenalty + 1;
            for (var i = 0, l = (op < ll ? op : ll); i < l; ++i) {
                lineFeedWidth += lines[i].lineFeedWidth;
            }
            pendings_.push({
                isParagraph: true,
                lineFeedWidth: lineFeedWidth,
                func: &() => {
                    core_.util.write(lines);
                },
            });
            flushParagraph();
        }

        public itemize(opts, items) {
            flushParagraph();
            core_.itemize.exec(opts, items);
        }

        private makeImageNumber() {
            var level = context_.imageCounter[0];
            var num = ++context_.imageCounter[1];
            var lt = [];
            for (var i = 1; i <= level; ++i) {
                lt.push(context_.sectionNumber[i]);
            }
            lt.push("" + num);
            return lt.join('.');
        }

        public imageCaption(caption) {
            var fontSize = context_.font.fontSize;
            var lineFeedWidth = fontSize * context_.page.lineFeedWidthRatio;

            caption = info_.base.image.label + ' ' + makeImageNumber() + ' ' + caption;
            core_.util.writeCenter({}, caption, context_.font.fontObj, fontSize, lineFeedWidth);

            info_.writer.lineBreak(lineFeedWidth);
        }

        public floatingImageCaption(caption, fontObj, fontSize, center, y) {
            if (!caption) {
                return;
            }
            caption = info_.base.image.label + ' ' + makeImageNumber() + ' ' + caption;
            var textWidth = info_.measure.textWidth(fontObj, fontSize, caption);
            var writer = info_.writer;
            var margin = writer.makeMarginSize();

            var x = center - textWidth / 2;
            var n = Linebreak.Box(0, new Word(info_).setWord(fontObj, fontSize, caption));
            writer.writeFloatingCaption(n, x, y);
        }

        public image(opts) {
            flushParagraph();
            if (opts.float.left || opts.float.right) {
                var fontSize = context_.font.fontSize;
                var fontObj = context_.font.fontObj;
                info_.writer.loadFloatingImage(opts.file, opts.scale, opts.float, opts.caption,
                    &(center, y) => floatingImageCaption(opts.caption, fontObj, fontSize, center, y));
                return;
            }

            info_.writer.imageCenter(opts.file, opts.scale);
            if (opts.caption) {
                imageCaption(opts.caption);
            } else {
                var fontSize = context_.font.fontSize;
                var lineFeedWidth = fontSize * context_.page.lineFeedWidthRatio;
                info_.writer.lineBreak(lineFeedWidth);
            }
        }

        public math(str) {
            flushParagraph();
            var img = Typesetting.Util.compileMath(str, true, info_.writer);
            info_.writer.imageCenter(img, 0.25);
        }

        private fontType(type) {
            switch (type) {
            case "Serif":
                context_.font.fontObj = fontObjSerif_;
                context_.font.fontObjBase = fontObjSerifBase_;
                break;
            case "Sans":
                context_.font.fontObj = fontObjSans_;
                context_.font.fontObjBase = fontObjSansBase_;
                break;
            case "Monotype":
                context_.font.fontObj = fontObjMonotype_;
                context_.font.fontObjBase = fontObjMonotypeBase_;
                break;
            }
        }

        public code(opts, text) {
            flushParagraph();

            [opts, text] = core_.util.makeOption(opts, text);
            var fontObj = context_.font.fontObj;
            var fontObjBase = context_.font.fontObjBase;
            fontType("Monotype");

            var margin = core_.util.makeMargin(opts);
            var fontSize = context_.font.fontSize;
            opts.margin.left = margin.left + fontSize;
            var texts = text.split(/\r?\n/);
            var tlast = texts.length() - 1;
            var lineFeedWidthBase = fontSize * 1.3;
            texts.each { &(line, i):
                var lineFeedWidth = i == tlast ? (lineFeedWidthBase + context_.page.paragraphSkipWidth) : lineFeedWidthBase;
                core_.util.writeLine(opts, line, context_.font.fontObj, fontSize, lineFeedWidth);
            };

            context_.font.fontObjBase = fontObjBase;
            context_.font.fontObj = fontObj;
        }

        public set(opts, func) {
            var fontObj = context_.font.fontObj;
            var fontObjBase = context_.font.fontObjBase;
            fontType(opts.fontType);
            core_.util.setOpts(opts);
            func();
            core_.util.setOpts(null);
            context_.font.fontObjBase = fontObjBase;
            context_.font.fontObj = fontObj;
        }

        public save(file) {
            flushParagraph();
            info_.writer.save(file);
        }
    }

} # namespace Typesetting
