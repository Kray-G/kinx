
_module _Enumerable {
    @isEnumerable = true;
    var host_ = this;
    var enumerator_, lazy_;
    var src_ = new Fiber(&() => host_.each());
    private initialize() {
        @collect = @map;
        @collectConcat = @flatMap;
        @inject = @reduce;
        @select = @findAll = @filter;
    }
    private genEnumerator() {
        if (!enumerator_) {
            enumerator_ = @keySet()
                .filter(&(e) => this[e].isFunction)
                .map(&(e) => [e, e == 'each' ? this._each : this[e]])
                .reduce(&(r, e) => {
                    r[e[0]] = e[1].isFunction ? e[1] : (_function(){});
                    return r;
                }, {});
        }
        return enumerator_;
    }
    public lazy() {
        lazy_ = true;
        return this;
    }
    public _each(func) {
        if (!lazy_) {
            return host_.each(func);
        }
        var count = 0;
        while (src_.isAlive()) {
            var v = src_.resume();
            if (!src_.isAlive()) {
                return false;
            }
            var r = func(v, count++);
            if (r.isDefined && !r) {
                return false;
            }
        }
    }
    private getTake(pf, n) {
        var r = [];
        while (pf.isAlive()) {
            var v = pf.resume();
            if (!pf.isAlive()) {
                break;
            }
            r.push(v);
            if (r.length() >= n) {
                break;
            }
        }
        return r;
    }
    private getTakeWhile(pf, func) {
        var r = [];
        while (pf.isAlive()) {
            var v = pf.resume();
            if (!pf.isAlive()) {
                break;
            }
            if (!func(v)) {
                break;
            }
            r.push(v);
        }
        return r;
    }
    private getDrop(pf, n) {
        var r = [], i = 0, f = false;
        while (pf.isAlive()) {
            var v = pf.resume();
            if (!pf.isAlive()) {
                break;
            }
            if (f) {
                r.push(v);
            } else if (++i > n) {
                r.push(v);
                f = true;
            }
        }
        return r;
    }
    private getDropWhile(pf, func) {
        var r = [], f = false;
        while (pf.isAlive()) {
            var v = pf.resume();
            if (!pf.isAlive()) {
                break;
            }
            if (f) {
                r.push(v);
            } else if (!func(v)) {
                r.push(v);
                f = true;
            }
        }
        return r;
    }
    private getAllElements(pf) {
        var r = [];
        while (pf.isAlive()) {
            var v = pf.resume();
            if (!pf.isAlive()) {
                break;
            }
            r.push(v);
        }
        return r;
    }
    public filter(func) {
        if (!lazy_) {
            var r = getAllElements(src_);
            return r.filter(func);
        }
        var pf = src_;
        src_ = new Fiber(&{
            while (pf.isAlive()) {
                var v = pf.resume();
                if (!pf.isAlive()) {
                    return;
                }
                if (func(v)) {
                    yield v;
                }
            }
        });
        return genEnumerator();
    }
    public reject(func) {
        if (!lazy_) {
            var r = getAllElements(src_);
            return r.reject(func);
        }
        var pf = src_;
        src_ = new Fiber(&{
            while (pf.isAlive()) {
                var v = pf.resume();
                if (!pf.isAlive()) {
                    return;
                }
                if (!func(v)) {
                    yield v;
                }
            }
        });
        return genEnumerator();
    }
    public map(func) {
        if (!lazy_) {
            var r = getAllElements(src_);
            return r.map(func);
        }
        var pf = src_;
        src_ = new Fiber(&{
            while (pf.isAlive()) {
                var v = pf.resume();
                if (!pf.isAlive()) {
                    return;
                }
                yield func(v);
            }
        });
        return genEnumerator();
    }
    public flatMap(func) {
        if (!lazy_) {
            var r = getAllElements(src_);
            return r.map(func).flatten();
        }
        var pf = src_;
        src_ = new Fiber(&{
            var r = [];
            while (pf.isAlive() || r.length() > 0) {
                if (r.length() > 0) {
                    yield r.shift();
                } else {
                    var v = pf.resume();
                    if (!pf.isAlive()) {
                        return;
                    }
                    v = func(v);
                    if (v.isArray) {
                        v = v.flatten();
                        v.each(&(e) => r.push(e));
                    } else {
                        yield v;
                    }
                }
            }
        });
        return genEnumerator();
    }
    public take(n) {
        if (!lazy_) {
            return getTake(src_, n);
        }
        var pf = src_;
        src_ = new Fiber(&{
            for (var i = 0; i < n && pf.isAlive(); ++i) {
                var v = pf.resume();
                if (!pf.isAlive()) {
                    break;
                }
                yield v;
            }
        });
        return genEnumerator();
    }
    public takeWhile(func) {
        if (!lazy_) {
            return getTakeWhile(src_, func);
        }
        var pf = src_;
        src_ = new Fiber(&{
            while (pf.isAlive()) {
                var v = pf.resume();
                if (!pf.isAlive()) {
                    break;
                }
                if (!func(v)) {
                    return;
                }
                yield v;
            }
        });
        return genEnumerator();
    }
    public drop(n) {
        if (!lazy_) {
            return getDrop(src_, n);
        }
        var pf = src_;
        src_ = new Fiber(&{
            for (var i = 0; i < n && pf.isAlive(); ++i) {
                pf.resume();
                if (!pf.isAlive()) {
                    break;
                }
            }
            while (pf.isAlive()) {
                var v = pf.resume();
                if (!pf.isAlive()) {
                    break;
                }
                yield v;
            }
        });
        return genEnumerator();
    }
    public dropWhile(func) {
        if (!lazy_) {
            return getDropWhile(src_, func);
        }
        var pf = src_;
        src_ = new Fiber(&{
            while (pf.isAlive()) {
                var v = pf.resume();
                if (!pf.isAlive()) {
                    break;
                }
                if (!func(v)) {
                    yield v;
                    break;
                }
            }
            while (pf.isAlive()) {
                var v = pf.resume();
                if (!pf.isAlive()) {
                    break;
                }
                yield v;
            }
        });
        return genEnumerator();
    }
    public reduce(func, initer) {
        var r = initer;
        while (src_.isAlive()) {
            var v = src_.resume();
            if (!src_.isAlive()) {
                break;
            }
            r = func(r, v);
        }
        return r;
    }
    public sort(func) {
        if (host_.end().isUndefined) {
            throw FiberException("Cannot complete the iteration");
        }
        var r = [];
        while (src_.isAlive()) {
            var v = src_.resume();
            if (!src_.isAlive()) {
                break;
            }
            r.push(v);
        }
        return r.sort(func);
    }
    public all(func) {
        if (host_.end().isUndefined) {
            throw FiberException("Cannot complete the iteration");
        }
        while (src_.isAlive()) {
            var v = src_.resume();
            if (!src_.isAlive()) {
                return true;
            }
            if (!func(v)) {
                break;
            }
        }
        return false;
    }
    public any(func) {
        if (host_.end().isUndefined) {
            throw FiberException("Cannot complete the iteration");
        }
        while (src_.isAlive()) {
            var v = src_.resume();
            if (!src_.isAlive()) {
                return false;
            }
            if (func(v)) {
                return true;
            }
        }
        return false;
    }
    public partition(func) {
        if (host_.end().isUndefined) {
            throw FiberException("Cannot complete the iteration");
        }
        var t = [];
        var f = [];
        while (src_.isAlive()) {
            var v = src_.resume();
            if (!src_.isAlive()) {
                break;
            }
            if (func(v)) {
                t.push(v);
            } else {
                f.push(v);
            }
        }
        return [t, f];
    }
    public sum() {
        if (host_.end().isUndefined) {
            throw FiberException("Cannot complete the iteration");
        }
        var s;
        while (src_.isAlive()) {
            v = src_.resume();
            if (!src_.isAlive()) {
                break;
            }
            s += v;
        }
        return s;
    }
    public toArray() {
        if (host_.end().isUndefined) {
            throw FiberException("Cannot complete the iteration");
        }
        var r = [];
        while (src_.isAlive()) {
            v = src_.resume();
            if (!src_.isAlive()) {
                break;
            }
            r.push(v);
        }
        return r;
    }
    public println() {
        while (src_.isAlive()) {
            var v = src_.resume();
            if (!src_.isAlive()) {
                return false;
            }
            System.println(v);
        }
    }
}

_Enumerable.filter = _function(predicate) {
    return _function(sequence) {
        return _function() {
            while (true) {
                const value = sequence();
                if (value.done) {
                    break;
                }
                if (predicate(value.value)) {
                    return value;
                }
            }
            return { done: true };
        };
    };
};
_Enumerable.reject = _function(predicate) {
    return _function(sequence) {
        return _function() {
            while (true) {
                const value = sequence();
                if (value.done) {
                    break;
                }
                if (!predicate(value.value)) {
                    return value;
                }
            }
            return { done: true };
        };
    };
};
_Enumerable.map = _function(mapping) {
    return _function(sequence) {
        return _function() {
            const value = sequence();
            if (!value.done) {
                return { value: mapping(value.value), done: false };
            }
            return { done: true };
        };
    };
};
_Enumerable.flatMap = _function(mapping) {
    return _function(sequence) {
        var r = [];
        return _function() {
            var active = true;
            while (active) {
                if (r.length() > 0) {
                    return r.shift();
                }
                const value = sequence();
                if (value.done) {
                    active = false;
                    break;
                }
                var mapped = mapping(value.value);
                if (mapped.isArray) {
                    mapped.flatten().each(_function(e) { return r.push({ value: e, done: false }); });
                } else {
                    return { value: mapped, done: false };
                }
            }
            return { done: true };
        };
    };
};
_Enumerable.take = _function(n) {
    return _function(sequence) {
        var count = 0;
        return _function() {
            if (count < n) {
                count++;
                return sequence();
            }
            return { done: true };
        };
    };
};
_Enumerable.takeWhile = _function(predicate) {
    return _function(sequence) {
        return _function() {
            const value = sequence();
            if (!value.done && predicate(value.value)) {
                return value;
            }
            return { done: true };
        };
    };
};
_Enumerable.drop = _function(n) {
    return _function(sequence) {
        var count = 0;
        return _function() {
            for ( ; count < n; ++count) {
                const value = sequence();
                if (value.done) {
                    count = n;
                    return { done: true };
                }
            }
            return sequence();
        };
    };
};
_Enumerable.dropWhile = _function(predicate) {
    return _function(sequence) {
        var active = true;
        return _function() {
            while (active) {
                const value = sequence();
                if (value.done) {
                    active = false;
                    return { done: true };
                }
                if (!predicate(value.value)) {
                    active = false;
                    return value;
                }
            }
            return sequence();
        };
    };
};
_Enumerable.each = _function(procedure) {
    return _function(sequence) {
        return _function() {
            var value = sequence();
            if (!value.done) {
                procedure(value.value);
            }
            return value;
        };
    };
};
_Enumerable.sort = _function(sequence) {
    var r = [];
    var value = sequence();
    while (!value.done) {
        r.push(value.value);
        value = sequence();
    }
    r = r.sort();
    var index = 0;
    return _function() {
        if (index < r.length()) {
            return { value: r[index++], done: false };
        }
        return { done: true };
    };
};
_Enumerable.shuffle = _function(sequence) {
    var r = [];
    var value = sequence();
    while (!value.done) {
        r.push(value.value);
        value = sequence();
    }
    r = r.shuffle();
    var index = 0;
    return _function() {
        if (index < r.length()) {
            return { value: r[index++], done: false };
        }
        return { done: true };
    };
};
_function printImpl(func, argument, value) {
    if (!value.done) {
        func(argument, value.value);
    }
    return value;
}
_Enumerable.print = _function(argument) {
    if (argument.isString) {
        return _function(sequence) {
            return _function() {
                return printImpl(System.print, argument, sequence());
            };
        };
    }
    const sequence = argument;
    return _function() {
        return printImpl(System.print, argument, sequence());
    };
};
_Enumerable.println = _function(argument) {
    if (argument.isString) {
        return _function(sequence) {
            return _function() {
                return printImpl(System.println, argument, sequence());
            };
        };
    }
    const sequence = argument;
    return _function() {
        return printImpl(System.println, argument, sequence());
    };
};
_Enumerable.trace = _function(value) {
    System.println(value);
    return value;
};

_Enumerable.any = _function(predicate) {
    return _function(sequence) {
        var value = sequence();
        while (!value.done) {
            if (predicate(value.value)) {
                return true;
            }
            value = sequence();
        }
        return false;
    };
};
_Enumerable.all = _function(predicate) {
    return _function(sequence) {
        var value = sequence();
        while (!value.done) {
            if (!predicate(value.value)) {
                return false;
            }
            value = sequence();
        }
        return true;
    };
};
_Enumerable.partition = _function(predicate) {
    return _function(sequence) {
        var t = [];
        var f = [];
        var value = sequence();
        while (!value.done) {
            if (predicate(value.value)) {
                t.push(value.value);
            } else {
                f.push(value.value);
            }
            value = sequence();
        }
        return [t, f];
    };
};
_Enumerable.sum = _function(sequence) {
    var result = 0;
    var value = sequence();
    while (!value.done) {
        result += value.value;
        value = sequence();
    }
    return result;
};
_Enumerable.toArray = _function(sequence) {
    const arr = [];
    var value = sequence();
    while (!value.done) {
        arr.push(value.value);
        value = sequence();
    }
    return arr;
};
_Enumerable.reduce = _function(accumulator, startValue) {
    return _function(sequence) {
        var result = startValue;
        var value = sequence();
        while (!value.done) {
            result = accumulator(result, value.value);
            value = sequence();
        }
        return result;
    };
};
_Enumerable.printlnAll = _function(sequence) {
    var value = sequence();
    while (!value.done) {
        System.println(value.value);
        value = sequence();
    }
};

_Enumerable.collect = _Enumerable.map;
_Enumerable.collectConcat = _Enumerable.flatMap;
_Enumerable.inject = _Enumerable.reduce;
_Enumerable.select = _Enumerable.findAll = _Enumerable.filter;

_class _RangeEnumerator(obj_) {
    var f_;
    private initialize() {
        f_ = new Fiber(&() => obj_.each());
    }
    public peek() {
        if (obj_.isEnded()) {
            throw StopIterationException("Enumerator has been ended");
        }
        return obj_.next();
    }
    public next() {
        return f_.resume();
    }
    public isEnded() {
        return !f_.isAlive();
    }
}

_class _ArrayEnumerator(obj_) {
    var i_, sz_;
    private initialize() {
        i_ = 0;
        sz_ = obj_.length();
    }
    public peek() {
        if (sz_ <= i_) {
            throw StopIterationException("Enumerator has been ended");
        }
        return obj_[i_];
    }
    public next() {
        return i_ < sz_ ? obj_[i_++] : i_++;
    }
    public isEnded() {
        return sz_ < i_;
    }
}

_class _FileEnumerator(file_) {
    var last_, prev_;
    public peek() {
        if (prev_.isUndefined) {
            prev_ = file_.readLine();
        }
        return prev_;
    }
    public next() {
        if (prev_.isDefined) {
            last_ = prev_;
            prev_ = null;
        } else {
            last_ = file_.readLine();
        }
        return last_;
    }
    public isEnded() {
        return last_.isDefined && !last_.isString;
    }
}

_class _ObjectEnumerator(obj_) {
    var ary_, i_, sz_;
    private initialize() {
        ary_ = obj_.keySet();
        sz_ = ary_.length();
        i_ = 0;
    }
    public peek() {
        if (sz_ <= i_) {
            throw StopIterationException("Enumerator has been ended");
        }
        var key = ary_[i_];
        var value = obj_[key];
        return [key, value];
    }
    public next() {
        if (i_ < sz_) {
            var key = ary_[i_++];
            var value = obj_[key];
            return [key, value];
        }
        ++i_;
    }
    public isEnded() {
        return sz_ < i_;
    }
}

_class _Enumerator(obj_) {
    mixin _Enumerable;
    var enumerator_;
    private initialize() {
        if (obj_.enumerator.isFunction) {
            enumerator_ = obj_.enumerator();
        } else if (obj_.isFile) {
            enumerator_ = new _FileEnumerator(obj_);
        } else if (obj_.isArray) {
            enumerator_ = new _ArrayEnumerator(obj_);
        } else if (obj_.isObject) {
            enumerator_ = new _ObjectEnumerator(obj_);
        } else {
            throw SystemException("Cannot create Enumerator");
        }
    }
    public peek() {
        return enumerator_.peek();
    }
    public next() {
        return enumerator_.next();
    }
    public isEnded() {
        return enumerator_.isEnded();
    }
    private eachByFunction(func) {
        while (true) {
            var n = enumerator_.next();
            if (enumerator_.isEnded()) {
                break;
            }
            func(n);
        }
    }
    public each(func) {
        if (func.isFunction) {
            return eachByFunction(func);
        }
        while (true) {
            var v = enumerator_.next();
            if (enumerator_.isEnded()) {
                break;
            }
            yield v;
        }
    }
}

_class _Range(start_, end_, excludeEnd_) {
    mixin _Enumerable;
    @Enumerable.sum = @sum;
    @isRange = true;
    var cur_;
    private initialize() {
        cur_ = start_;
    }
    public enumerator() {
        return new _RangeEnumerator(@lazy());
    }
    public begin() {
        return start_;
    }
    public end() {
        return end_;
    }
    public isEndExcluded() {
        return excludeEnd_;
    }
    public next() {
        return cur_.next();
    }
    private comp(a, b) {
        if (a.isString && b.isString) {
            var al = a.length();
            var bl = b.length();
            if (al < bl) {
                return -1;
            }
            if (al > bl) {
                return 1;
            }
        }
        return a <=> b;
    }
    private eachByFunction(func) {
        if (@array) {
            var i = 0;
            var sz = @array.length();
            while (i < sz) {
                var r = func(@array[i], i);
                if (r.isDefined && !r) {
                    break;
                }
                ++i;
            }
            return i;
        }
        var i = 0;
        while (true) {
            if (end_ && excludeEnd_ && comp(cur_, end_) == 0) {
                break;
            }
            var r = func(cur_, i);
            if (r.isDefined && !r) {
                break;
            }
            if (end_ && !excludeEnd_ && comp(cur_, end_) == 0) {
                break;
            }
            cur_ = cur_.next();
            ++i;
        }
        return i;
    }
    public each(func) {
        if (start_.isUndefined) {
            throw FiberException("Cannot iterate the range because of no start value");
        }
        if (start_.isDefined && end_.isDefined) {
            if (excludeEnd_ && (comp(start_, end_) >= 0)) {
                return false;
            }
            if (!excludeEnd_ && (comp(start_, end_) > 0)) {
                return false;
            }
        }
        if (func.isFunction) {
            return eachByFunction(func);
        }
        if (@array) {
            @array.each(&(e) => {
                yield e;
            });
            return false;
        }
        while (true) {
            if (end_.isDefined && excludeEnd_) {
                if (comp(cur_, end_) >= 0) {
                    return false;
                }
            }
            yield cur_;
            if (end_.isDefined && !excludeEnd_) {
                if (comp(cur_, end_) >= 0) {
                    return false;
                }
            }
            cur_ = cur_.next();
        }
    }
    public sum(func) {
        if (func.isDefined || !(start_.isInteger && end_.isInteger)) {
            return @Enumerable.sum(func);
        }
        var end = excludeEnd_ ? (end_ - 1) : end_;
        if (end < start_) {
            return 0;
        }
        return Integer.parseInt((end + start_) * (end - start_ + 1) / 2);
    }
    public ==(rhs) {
        if (start_.isDefined && comp(start_, rhs) > 0) {
            return false;
        }
        if (end_.isDefined) {
            if (excludeEnd_) {
                if (comp(end_, rhs) <= 0) {
                    return false;
                }
            } else {
                if (comp(end_, rhs) < 0) {
                    return false;
                }
            }
        }
        return true;
    }
    public [](rhs) {
        if (!@array) {
            @array = @toArray();
        }
        return @array[rhs];
    }
}
